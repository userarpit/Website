<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <title>Cobol + DB2 + CICS</title>
  <meta name="description" content="Cobol + DB2 + CICS" />
  <meta name="revised" content="08/07/2022" />
  <meta name="author" content = "Arpit Khandelwal" />
  <meta http-equiv = "Content-Type" content = "text/html; charset = UTF-8" />
  <meta http-equiv = "Content-Script-Type" content = "text/JavaScript" />
  <link rel = "stylesheet" type = "text/css" href = "../css/index.css">
  <script type = "text/javascript" src = "../javascript/index.js"></script>
  <noscript>
    JavaScript is disabled. Please enabled it.
  </noscript>
</head>
    <body>

<table>
    <tr>
        
        <td><span style="font-size:20px; color:rgb(30, 156, 235)">Cobol + DB2 + CICS</span></td>
    </tr>
</table>

<form>
<textarea  id="coboldb2cicstextarea" rows="22" cols="50">
       IDENTIFICATION DIVISION.
       PROGRAM-ID. BANKDTAC.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       COPY BANKDTA.
       COPY DFHBMSCA.
       COPY DFHAID.
           EXEC SQL INCLUDE SQLCA END-EXEC.
           EXEC SQL INCLUDE CUSTARPI END-EXEC.
       01 WS-VARIABLES.
           05 WS-DTTM          PIC S9(15) COMP-3.
           05 WS-COMMAREA      PIC X(01).
           05 WS-BYE           PIC X(03) VALUE 'BYE'.
           05 WS-SQLCODE       PIC +(8)9.
       01 WS-CONDITIONAL.
           05 WS-VALIDATION-STATUS          PIC X.
               88 VALIDATION-SUCCESSFUL     VALUE 'Y'.
               88 VALIDATION-NOT-SUCCESSFUL VALUE 'N'.
       01 WS-EMPLOYEE-REC.
           05 WS-ENO       PIC X(5).
           05 WS-ENAME     PIC X(20).
           05 WS-EADD      PIC X(20).
           05 WS-EBAL      PIC 9(5).
           05 WS-EDOB      PIC X(10).
       LINKAGE SECTION.
       01 DFHCOMMAREA      PIC X(01).
       PROCEDURE DIVISION.
           IF EIBCALEN = 0
               MOVE LOW-VALUES TO BANKDTMI BANKDTMO
               MOVE 'ENTER FIELDS' TO MSGO
               PERFORM 1000-SEND-MAP
           ELSE
               PERFORM 2000-RECEIVE-MAP
           END-IF.
           EXEC CICS RETURN
               TRANSID('BANK')
               COMMAREA(WS-COMMAREA)
               LENGTH(LENGTH OF WS-COMMAREA)
           END-EXEC.
       1000-SEND-MAP.
           PERFORM 1050-DATE-TIME-PARA.
           EXEC CICS SEND
               MAP('BANKDTM')
               MAPSET('BANKDTA')
               FROM(BANKDTMO)
               ERASE
           END-EXEC.
       1050-DATE-TIME-PARA.
           EXEC CICS ASKTIME
                    ABSTIME(WS-DTTM)
           END-EXEC.
           EXEC CICS FORMATTIME
                     ABSTIME(WS-DTTM)
                     DDMMYYYY(DTO)
                     DATESEP('-')
                     TIME(TMO)
                     TIMESEP(':')
           END-EXEC.
       2000-RECEIVE-MAP.
           EXEC CICS RECEIVE
               MAP('BANKDTM')
               MAPSET('BANKDTA')
               INTO(BANKDTMI)
               ASIS
           END-EXEC.
           PERFORM 2100-FKEY-CHECK-PARA.
           PERFORM 1000-SEND-MAP.
       2100-FKEY-CHECK-PARA.
           EVALUATE EIBAID
               WHEN DFHPF1
                   PERFORM 2110-ADD-DATA
               WHEN DFHPF2
                   PERFORM 2120-DEL-DATA
               WHEN DFHPF3
                   PERFORM 2130-UPD-DATA
               WHEN DFHPF4
                   PERFORM 2140-SEL-DATA
               WHEN DFHPF5
                   MOVE LOW-VALUES TO BANKDTMI BANKDTMO
                   MOVE 'REFRESH IS DONE' TO MSGO
               WHEN DFHPF6
                   EXEC CICS SEND
                       FROM(WS-BYE)
                       LENGTH(LENGTH OF WS-BYE)
                       ERASE
                   END-EXEC
                   EXEC CICS RETURN END-EXEC
           END-EVALUATE.
       2110-ADD-DATA.
           SET VALIDATION-SUCCESSFUL TO TRUE.
           PERFORM 2111-VALIDATION.
           IF VALIDATION-NOT-SUCCESSFUL
               CONTINUE
           ELSE
               MOVE ENOI   TO HV-CNO
               MOVE ENAMEI TO HV-CNAME
               MOVE EADDI  TO HV-CADD-TEXT
               MOVE LENGTH OF HV-CADD-TEXT TO HV-CADD-LEN
               MOVE EBALI  TO HV-CBAL
               MOVE EDOBI  TO HV-CDOB

               EXEC SQL
                   INSERT INTO CUST_ARPIT VALUES(:HV-CNO, :HV-CNAME,
                   :HV-CADD, :HV-CBAL, :HV-CDOB)
               END-EXEC
               MOVE SQLCODE TO WS-SQLCODE
               EVALUATE SQLCODE
                   WHEN 000
                       MOVE 'INSERT SUCCESSFUL' TO MSGO
                   WHEN -803
                       MOVE 'DUPLICATE'         TO MSGO
                   WHEN OTHER
                       STRING 'ERROR IN INSERT ' DELIMITED BY SIZE,
                              WS-SQLCODE DELIMITED BY SIZE
                                  INTO MSGO
               END-EVALUATE
           END-IF.
       2111-VALIDATION.
           IF ENAMEI IS NUMERIC
               MOVE 'NAME CAN NOT BE NUMERIC' TO MSGO
           END-IF.
       2120-DEL-DATA.
           MOVE ENOI   TO HV-CNO.
           EXEC SQL
               SELECT CNAME,CADD,CBAL,CDOB INTO
               :HV-CNAME, :HV-CADD, :HV-CBAL, :HV-CDOB
               FROM CUST_ARPIT WHERE CNO = :HV-CNO
           END-EXEC.
           MOVE SQLCODE TO WS-SQLCODE.
           EVALUATE SQLCODE
               WHEN 000
                   MOVE HV-CNAME TO ENAMEO
                   MOVE HV-CADD-TEXT TO EADDO
                   MOVE HV-CADD-LEN TO EADDL
                   MOVE HV-CBAL TO EBALO
                   MOVE HV-CDOB TO EDOBO
                   MOVE 'PRESS ANY KEY TO DELETE' TO MSGO
                   PERFORM 1000-SEND-MAP
                   PERFORM 2121-RECEIVE-FOR-DEL
                   PERFORM 2122-DELETE
               WHEN +100
                   MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                       STRING 'ERROR IN SELECT ' DELIMITED BY SIZE,
                              WS-SQLCODE DELIMITED BY SIZE
                                  INTO MSGO
           END-EVALUATE.
       2121-RECEIVE-FOR-DEL.
           EXEC CICS RECEIVE
               MAP('BANKDTM')
               MAPSET('BANKDTA')
               INTO(BANKDTMI)
               ASIS
           END-EXEC.
       2122-DELETE.
           EXEC SQL
               DELETE FROM CUST_ARPIT WHERE
               CNO = :HV-CNO
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 000
                   MOVE 'DELETE IS SUCCESSFUL' TO MSGO
               WHEN +100
                   MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                       STRING 'ERROR IN DELETE ' DELIMITED BY SIZE,
                              WS-SQLCODE DELIMITED BY SIZE
                                  INTO MSGO
           END-EVALUATE.
       2130-UPD-DATA.
           IF ENOI IS EQUAL SPACES
               MOVE 'ENTER EMPLOYEE NUMBER' TO MSGO
               MOVE -1 TO ENOL
           ELSE
               MOVE ENOI TO HV-CNO
               EXEC SQL
                   SELECT CNAME,CADD,CBAL,CDOB INTO
                   :HV-CNAME, :HV-CADD, :HV-CBAL, :HV-CDOB
                   FROM CUST_ARPIT WHERE CNO = :HV-CNO
               END-EXEC
               MOVE SQLCODE TO WS-SQLCODE
               EVALUATE SQLCODE
                   WHEN 000
                       MOVE HV-CNAME TO ENAMEO
                       MOVE HV-CADD-TEXT TO EADDO
                       MOVE HV-CADD-LEN TO EADDL
                       MOVE HV-CBAL TO EBALO
                       MOVE HV-CDOB TO EDOBO
                       MOVE 'DO CHANGES AND PRESS ANY KEY' TO MSGO
                       PERFORM 1000-SEND-MAP
                       PERFORM 2131-RECEIVE-FOR-UPD
                       PERFORM 2132-UPDATE
                   WHEN +100
                       MOVE 'RECORD NOT FOUND' TO MSGO
                   WHEN OTHER
                       STRING 'ERROR IN SELECT ' DELIMITED BY SIZE,
                              WS-SQLCODE DELIMITED BY SIZE
                                  INTO MSGO
               END-EVALUATE
           END-IF.
       2131-RECEIVE-FOR-UPD.
           EXEC CICS RECEIVE
               MAP('BANKDTM')
               MAPSET('BANKDTA')
               INTO(BANKDTMI)
               ASIS
           END-EXEC.
       2132-UPDATE.
           MOVE ENAMEI TO HV-CNAME.
           MOVE EADDI  TO HV-CADD-TEXT.
           MOVE LENGTH OF HV-CADD-TEXT TO HV-CADD-LEN.
           MOVE EBALI  TO HV-CBAL.
           MOVE EDOBI  TO HV-CDOB.
           EXEC SQL
               UPDATE CUST_ARPIT SET CNAME = :HV-CNAME,
               CADD = :HV-CADD,CBAL = :HV-CBAL,CDOB = :HV-CDOB
               WHERE CNO = :HV-CNO
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 000
                   MOVE 'UPDATE IS SUCCESSFUL' TO MSGO
               WHEN +100
                   MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                   STRING 'ERROR IN UPDATE ' DELIMITED BY SIZE,
                          WS-SQLCODE DELIMITED BY SIZE
                              INTO MSGO
           END-EVALUATE.
       2140-SEL-DATA.
           MOVE ENOI TO HV-CNO.
           EXEC SQL
               SELECT CNAME,CADD,CBAL,CDOB INTO
               :HV-CNAME, :HV-CADD, :HV-CBAL, :HV-CDOB
               FROM CUST_ARPIT WHERE CNO = :HV-CNO
           END-EXEC.
           MOVE SQLCODE TO WS-SQLCODE.
           EVALUATE SQLCODE
               WHEN 000
                   MOVE HV-CNAME TO ENAMEO
                   MOVE HV-CADD-TEXT TO EADDO
                   MOVE HV-CADD-LEN TO EADDL
                   MOVE HV-CBAL TO EBALO
                   MOVE HV-CDOB TO EDOBO
                   MOVE 'RECORD SELECTED' TO MSGO
               WHEN +100
                   MOVE LOW-VALUES TO BANKDTMI BANKDTMO
                   MOVE HV-CNO TO ENOO
                   MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                   STRING 'ERROR IN SELECT ' DELIMITED BY SIZE,
                          WS-SQLCODE DELIMITED BY SIZE
                              INTO MSGO
           END-EVALUATE.
  </textarea>
</form>
   
</body>
</html>