<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <title>COBOL program to handle CICS bank map</title>
  <meta name="description" content="COBOL program to handle CICS bank map" />
  <meta name="revised" content="08/07/2022" />
  <meta name="author" content = "Arpit Khandelwal" />
  <meta http-equiv = "Content-Type" content = "text/html; charset = UTF-8" />
  <meta http-equiv = "Content-Script-Type" content = "text/JavaScript" />
  <link rel = "stylesheet" type = "text/css" href = "../css/index.css">
  <script type = "text/javascript" src = "../javascript/index.js"></script>
  <noscript>
    JavaScript is disabled. Please enabled it.
  </noscript>
</head>
    <body>

<table>
    <tr>
        
        <td><span style="font-size:20px; color:rgb(30, 156, 235)">COBOL program to handle CICS bank map</span></td>
    </tr>
</table>
<form>
<textarea  id="coboldb2cicstextarea" rows="22" cols="50">
       ID DIVISION.
       PROGRAM-ID. BANK.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       COPY ARPIBNK.
       COPY DFHBMSCA.
       COPY DFHAID.
       01 WS-DTTM        PIC S9(15) COMP-3.
       01 WS-COMMAREA    PIC X(01).
       01 WS-FILE-OPEN   PIC X(22) VALUE 'SET FILE(APFILE1) OPEN'.
       01 WS-FILE-CLOSE  PIC X(24) VALUE 'SET FILE(APFILE1) CLOSED'.
       01 WS-BYE         PIC X(03) VALUE 'BYE'.
       01 WS-RESP        PIC S9(8) COMP.
       01 WS-CUS-REC.
           05 WS-CNO     PIC 9(5).
           05 WS-CNAME   PIC X(20).
           05 WS-CADD    PIC X(25).
           05 WS-CBAL    PIC 9(5).
       LINKAGE SECTION.
       01 DFHCOMMAREA    PIC X(01).
       PROCEDURE DIVISION.
      *    EXEC CICS DELAY TIME(000150) END-EXEC.
           IF EIBCALEN = 0 THEN
               EXEC CICS LINK PROGRAM('DFHEMTP')
               COMMAREA(WS-FILE-OPEN)
               LENGTH(LENGTH OF WS-FILE-OPEN)
               END-EXEC
               MOVE LOW-VALUES TO ARPIBNMI ARPIBNMO
               MOVE 'ENTER FIELDS' TO MSGO
               PERFORM 1000-SEND-BANK-MAP
           ELSE
               PERFORM 2000-RECEIVE-BANK-MAP
           END-IF.
           EXEC CICS RETURN
               TRANSID('BANK')
               COMMAREA(WS-COMMAREA)
               LENGTH(LENGTH OF WS-COMMAREA)
           END-EXEC.
       1000-SEND-BANK-MAP.
           PERFORM 1050-DATE-TIME-PARA.
           EXEC CICS SEND
               MAP('ARPIBNM')
               MAPSET('ARPIBNK')
               FROM(ARPIBNMO)
               ERASE
           END-EXEC.
       1050-DATE-TIME-PARA.
           EXEC CICS ASKTIME
                    ABSTIME(WS-DTTM)
           END-EXEC.
           EXEC CICS FORMATTIME
                     ABSTIME(WS-DTTM)
                     DDMMYYYY(DTO)
                     DATESEP('-')
                     TIME(TMO)
                     TIMESEP(':')
           END-EXEC.
       2000-RECEIVE-BANK-MAP.
           EXEC CICS RECEIVE
               MAP('ARPIBNM')
               MAPSET('ARPIBNK')
               INTO(ARPIBNMI)
               ASIS
           END-EXEC.

           PERFORM 2100-FKEY-CHECK-PARA.
           PERFORM 1000-SEND-BANK-MAP.
       2100-FKEY-CHECK-PARA.
           EVALUATE EIBAID
               WHEN DFHPF1
                   PERFORM 2110-ADD-DATA
               WHEN DFHPF2
                   PERFORM 2120-SEL-DATA
               WHEN DFHPF3
                   PERFORM 2130-UPD-DATA
               WHEN DFHPF4
                   PERFORM 2140-DEL-DATA
               WHEN DFHPF5
                   MOVE LOW-VALUES TO ARPIBNMI ARPIBNMO
                   MOVE 'REFRESH IS DONE' TO MSGO
               WHEN DFHPF6
               EXEC CICS LINK PROGRAM('DFHEMTP')
               COMMAREA(WS-FILE-CLOSE)
               LENGTH(LENGTH OF WS-FILE-CLOSE)
               END-EXEC
                   EXEC CICS SEND
                       FROM(WS-BYE)
                       LENGTH(LENGTH OF WS-BYE)
                       ERASE
                   END-EXEC
                   EXEC CICS RETURN END-EXEC
           END-EVALUATE.
       2110-ADD-DATA.
           IF CNOI IS NUMERIC THEN
               MOVE CNOI TO WS-CNO
           END-IF.
           MOVE CNAMEI TO WS-CNAME.
           MOVE CADDI  TO WS-CADD.
           MOVE CBALI  TO WS-CBAL.
           EXEC CICS WRITE
               FILE('APFILE1')
               FROM(WS-CUS-REC)
               RIDFLD(WS-CNO)
               LENGTH(LENGTH OF WS-CUS-REC)
               RESP(WS-RESP)
           END-EXEC.
           EVALUATE WS-RESP
               WHEN DFHRESP(NORMAL)
                  MOVE 'WRITE IS DONE' TO MSGO
               WHEN DFHRESP(DUPREC)
                  MOVE 'DUPLICATE KEY' TO MSGO
               WHEN OTHER
                  MOVE 'ERROR IN WRITE' TO MSGO
           END-EVALUATE.
       2120-SEL-DATA.
           MOVE CNOI TO WS-CNO.
           EXEC CICS READ
               FILE('APFILE1')
               INTO(WS-CUS-REC)
               RIDFLD(WS-CNO)
               LENGTH(LENGTH OF WS-CUS-REC)
               KEYLENGTH(LENGTH OF WS-CNO)
               RESP(WS-RESP)
           END-EXEC.
           EVALUATE WS-RESP
               WHEN DFHRESP(NORMAL)
                   MOVE WS-CNO   TO CNOO
                   MOVE WS-CNAME TO CNAMEO
                   MOVE WS-CADD  TO CADDO
                   MOVE WS-CBAL  TO CBALO
                  MOVE 'READ IS DONE' TO MSGO
               WHEN DFHRESP(NOTFND)
                  MOVE LOW-VALUES TO ARPIBNMI ARPIBNMO
                  MOVE WS-CNO   TO CNOO
                  MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                  MOVE 'ERROR IN READ' TO MSGO
           END-EVALUATE.
       2130-UPD-DATA.
           MOVE CNOI TO WS-CNO.
           EXEC CICS READ
               FILE('APFILE1')
               INTO(WS-CUS-REC)
               RIDFLD(WS-CNO)
               LENGTH(LENGTH OF WS-CUS-REC)
               KEYLENGTH(LENGTH OF WS-CNO)
               RESP(WS-RESP)
               UPDATE
           END-EXEC.
           EVALUATE WS-RESP
               WHEN DFHRESP(NORMAL)
                   MOVE WS-CNO   TO CNOO
                   MOVE WS-CNAME TO CNAMEO
                   MOVE WS-CADD  TO CADDO
                   MOVE WS-CBAL  TO CBALO
                   MOVE 'READ IS DONE, DO CHANGES' TO MSGO
                   PERFORM 1000-SEND-BANK-MAP
                   PERFORM 2131-RECEIVE-MAP-AFTER-CHANGE
                   PERFORM 2132-REWRITE
               WHEN DFHRESP(NOTFND)
                  MOVE LOW-VALUES TO ARPIBNMI ARPIBNMO
                  MOVE WS-CNO   TO CNOO
                  MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                  MOVE 'ERROR IN READ' TO MSGO
           END-EVALUATE.
       2140-DEL-DATA.
           MOVE CNOI TO WS-CNO.
           EXEC CICS READ
               FILE('APFILE1')
               INTO(WS-CUS-REC)
               RIDFLD(WS-CNO)
               LENGTH(LENGTH OF WS-CUS-REC)
               KEYLENGTH(LENGTH OF WS-CNO)
               RESP(WS-RESP)
               UPDATE
           END-EXEC.
           EVALUATE WS-RESP
               WHEN DFHRESP(NORMAL)
                  PERFORM DELETE-DATA
               WHEN DFHRESP(NOTFND)
                  MOVE LOW-VALUES TO ARPIBNMI ARPIBNMO
                  MOVE WS-CNO   TO CNOO
                  MOVE 'RECORD NOT FOUND' TO MSGO
               WHEN OTHER
                  MOVE 'ERROR IN READ' TO MSGO
           END-EVALUATE.

       2131-RECEIVE-MAP-AFTER-CHANGE.
           EXEC CICS RECEIVE
               MAP('ARPIBNM')
               MAPSET('ARPIBNK')
               INTO(ARPIBNMI)
           END-EXEC.
       2132-REWRITE.
           IF CNOI IS NUMERIC THEN
               MOVE CNOI TO WS-CNO
           END-IF.
           MOVE CNAMEI TO WS-CNAME.
           MOVE CADDI  TO WS-CADD.
           MOVE CBALI  TO WS-CBAL.
           EXEC CICS REWRITE
               FILE('APFILE1')
               FROM(WS-CUS-REC)
               LENGTH(LENGTH OF WS-CUS-REC)
               RESP(WS-RESP)
           END-EXEC.
           EVALUATE WS-RESP
               WHEN DFHRESP(NORMAL)
                  MOVE 'REWRITE IS DONE' TO MSGO
               WHEN DFHRESP(INVREQ)
                  MOVE 'REWRITE WITHOUT READ WITH UPDATE' TO MSGO
               WHEN OTHER
                  MOVE 'ERROR IN WRITE' TO MSGO
           END-EVALUATE.

       DELETE-DATA.
           EXEC CICS DELETE
               FILE('APFILE1')
               RIDFLD(WS-CNO)
               RESP(WS-RESP)
           END-EXEC

           EVALUATE WS-RESP
              WHEN DFHRESP(NORMAL)
                  MOVE 'RECORD DELETED' TO MSGO
              WHEN DFHRESP(INVREQ)
                  MOVE 'READ WITHOUT UPDATE' TO MSGO
              WHEN OTHER
                  MOVE 'ERROR IN DELETE' TO MSGO
           END-EVALUATE.
  </textarea>
</form>
   
</body>
</html>